package grails.plugin.cache

import org.grails.datastore.mapping.config.Settings
import org.grails.datastore.mapping.multitenancy.MultiTenancySettings
import org.grails.datastore.mapping.multitenancy.resolvers.SystemPropertyTenantResolver
import org.grails.datastore.mapping.simple.SimpleMapDatastore
import org.grails.plugin.cache.GrailsCacheManager
import org.springframework.cache.Cache
import org.springframework.cache.CacheManager
import spock.lang.Specification

/**
 * Created by graemerocher on 18/04/2017.
 */
class CacheableParseSpec extends Specification {

    void "test declare condition in closure"() {
        given:
        Class testService = new GroovyShell().evaluate('''
import grails.plugin.cache.*
import groovy.transform.CompileStatic

@CompileStatic
class TestService {
    @Cacheable(value = 'basic', condition = { x < 10 })
    def multiply(int x, int y) {
        x * y
    }
    
    @Cacheable(value = 'sum')
    def sum(int x, int y) {
        x + y
    }
}
return TestService

''')
        def instance = testService.newInstance()
        expect:
        instance.multiply(1,2) ==2
    }

    void "test include tenant id when used with @CurrentTenant"() {
        given:
        def config = [(Settings.SETTING_MULTI_TENANCY_MODE): MultiTenancySettings.MultiTenancyMode.DISCRIMINATOR,
                      (Settings.SETTING_MULTI_TENANT_RESOLVER): new SystemPropertyTenantResolver()]
        def datastore = new SimpleMapDatastore(config, getClass().getPackage())

        Class testService = new GroovyShell().evaluate('''

import grails.gorm.multitenancy.CurrentTenant
import grails.plugin.cache.*
import groovy.transform.CompileStatic

@CompileStatic
class TestService {
    @Cacheable('basic')
    @CurrentTenant
    def multiply(int x, int y) {
        x * y
    }
}
return TestService

''')
        def instance = testService.newInstance()
        GrailsCacheManager cacheManager = Stub(GrailsCacheManager)
        instance.@"org_grails_plugin_cache_GrailsCacheManagerAware__grailsCacheManager" = cacheManager
        GrailsCacheKeyGenerator keyGenerator = Mock(GrailsCacheKeyGenerator)
        instance.@"org_grails_plugin_cache_GrailsCacheManagerAware__customCacheKeyGenerator" = keyGenerator

        when:
        System.setProperty(SystemPropertyTenantResolver.PROPERTY_NAME, "test")
        instance.multiply(1,2)
        cacheManager.getCache("basic") >> Stub(Cache)

        then:
        1 * keyGenerator.generate("TestService", "multiply", _, [x:1, y:2, tenantId: "test"])


        cleanup:
        System.setProperty(SystemPropertyTenantResolver.PROPERTY_NAME, "")
        datastore?.close()
    }


    void "test include tenant id when used with @CurrentTenant and @Transactional"() {
        given:
        def config = [(Settings.SETTING_MULTI_TENANCY_MODE): MultiTenancySettings.MultiTenancyMode.DISCRIMINATOR,
                      (Settings.SETTING_MULTI_TENANT_RESOLVER): new SystemPropertyTenantResolver()]
        def datastore = new SimpleMapDatastore(config, getClass().getPackage())

        Class testService = new GroovyShell().evaluate('''

import grails.gorm.multitenancy.CurrentTenant
import grails.gorm.transactions.Transactional
import grails.plugin.cache.*
import groovy.transform.CompileStatic

@CompileStatic
class TestService {
    @Cacheable('basic')
    @CurrentTenant
    @Transactional
    def multiply(int x, int y) {
        x * y
    }
}
return TestService

''')
        def instance = testService.newInstance()
        GrailsCacheManager cacheManager = Stub(GrailsCacheManager)
        instance.@"org_grails_plugin_cache_GrailsCacheManagerAware__grailsCacheManager" = cacheManager
        GrailsCacheKeyGenerator keyGenerator = Mock(GrailsCacheKeyGenerator)
        instance.@"org_grails_plugin_cache_GrailsCacheManagerAware__customCacheKeyGenerator" = keyGenerator

        when:
        System.setProperty(SystemPropertyTenantResolver.PROPERTY_NAME, "test")
        cacheManager.getCache("basic") >> Stub(Cache)
        instance.multiply(1,2)

        then:
        1 * keyGenerator.generate("TestService", "multiply", _, [x:1, y:2, tenantId: "test"])


        cleanup:
        System.setProperty(SystemPropertyTenantResolver.PROPERTY_NAME, "")
        datastore?.close()
    }
}
